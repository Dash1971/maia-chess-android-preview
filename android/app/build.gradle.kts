import java.security.MessageDigest

plugins {
    id("com.android.application")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

val releaseKeystorePath = System.getenv("MOBILE_MAIA_KEYSTORE")
val releaseStorePassword = System.getenv("MOBILE_MAIA_STORE_PASSWORD")
val releaseKeyPassword = System.getenv("MOBILE_MAIA_KEY_PASSWORD")
val releaseSigningValues = listOf(
    releaseKeystorePath,
    releaseStorePassword,
    releaseKeyPassword,
)

require(releaseSigningValues.all { it == null } || releaseSigningValues.all { it != null }) {
    "Set MOBILE_MAIA_KEYSTORE, MOBILE_MAIA_STORE_PASSWORD, and " +
        "MOBILE_MAIA_KEY_PASSWORD together."
}

dependencies {
    implementation("com.microsoft.onnxruntime:onnxruntime-android:1.24.3")
    androidTestImplementation("androidx.test.ext:junit:1.2.1")
    androidTestImplementation("androidx.test:runner:1.6.2")
}

android {
    namespace = "com.dash1971.maia_chess"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId = "com.dash1971.maia_chess.preview"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        // beta.20's split ARM64 APK installed as versionCode 2074 because
        // Flutter added its ABI offset. Universal releases must therefore use
        // pubspec build number 2075 or greater to remain upgrade-compatible.
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (releaseKeystorePath != null) {
            create("mobileMaiaRelease") {
                storeFile = file(releaseKeystorePath)
                storePassword = releaseStorePassword
                keyAlias = "mobile-maia"
                keyPassword = releaseKeyPassword
            }
        }
    }

    buildTypes {
        release {
            // Reproducible builders produce an unsigned release when signing
            // credentials are absent. Official Preview releases provide all
            // three variables.
            signingConfig = signingConfigs.findByName("mobileMaiaRelease")
            // Remove unused Java/Kotlin bytecode, including Flutter's dormant
            // deferred-component bridge. JNI/reflection-sensitive ONNX classes
            // remain protected by the explicit rules in proguard-rules.pro.
            isMinifyEnabled = true
            isShrinkResources = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro",
            )
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
    }
}

flutter {
    source = "../.."
}

// Fail before Flutter copies assets: GitHub ZIP downloads and incomplete LFS
// clones otherwise produce an installable APK with a tiny text pointer as Maia.
val verifyMaiaModel by tasks.registering {
    val model = rootProject.file("../assets/models/maia3-79m.onnx")
    inputs.file(model)
    doLast {
        require(model.isFile && model.length() == 316_034_244L) {
            "Maia model missing or wrong size. Run git lfs install && git lfs pull."
        }
        val digest = MessageDigest.getInstance("SHA-256")
        model.inputStream().buffered().use { stream ->
            val buffer = ByteArray(1024 * 1024)
            while (true) {
                val count = stream.read(buffer)
                if (count < 0) break
                digest.update(buffer, 0, count)
            }
        }
        val sha256 = digest.digest().joinToString("") { "%02x".format(it) }
        require(sha256 == "3454b03ae78baa64a87b345fdb1a457265d912caec531039b074f07eda0d8010") {
            "Maia model SHA-256 mismatch. Restore the pinned Git LFS object."
        }
    }
}
tasks.matching { it.name == "preBuild" || it.name.startsWith("compileFlutterBuild") }.configureEach {
    dependsOn(verifyMaiaModel)
}
